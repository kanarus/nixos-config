setopt INTERACTIVE_COMMENTS

# enable Ctrl-{left, right} to move by words 
bindkey ";5C" forward-word
bindkey ";5D" backward-word

# enable {up, down} to complete only with history matching current input & move cursor to end
bindkey "^[OA" history-beginning-search-backward
bindkey "^[OB" history-beginning-search-forward

function self_insert_with_log() {
  echo "KEYS='$KEYS'" >> ~/debug2.log
  zle .self-insert
}
zle -N self-insert self_insert_with_log

# prompt style
function git_status_color() {
  git_status_output=$(git status --short)
  if [ -z $git_status_output ]; then
    echo '157'
  else
    echo '197'
  fi
}
function maybe_git_branch() {
  git_output=$(git symbolic-ref --short HEAD 2>&1)
  if [[ $git_output =~ '^fatal: ' ]]; then
    echo ''
  else
    echo '(%F{'$(git_status_color)'}'$git_output'%F{153})'
  fi
}
setopt PROMPT_SUBST
export PS1='%F{153}[%n%F{111}@%m%F{153}:%~]$(maybe_git_branch)%f '

# aliases
alias helix='hx'
alias la='ls -al'

# abbreviations
typeset -Ag abbreviations
abbreviations=(
  "ns"   "sudo nixos-rebuild switch --flake ~/nixos-config"
  "com"  "git add . && git commit -m"
  "po"   "git push origin"
  "push" "git push"
  "x"    "helix"
)
function expand_abbreviation() {
  local MATCH
  setopt EXTENDED_GLOB
  LBUFFER="${LBUFFER%%(#m)[_a-zA-Z0-9]#}"
  unsetopt EXTENDED_GLOB
  LBUFFER+="${abbreviations[$MATCH]:-$MATCH}"
  zle self-insert
}
zle -N expand_abbreviation
bindkey " " expand_abbreviation
bindkey -M isearch " " self-insert      
